plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'idea'
    id "com.modrinth.minotaur" version "2.+"
    id "me.hypherionmc.cursegradle" version "2.0.1"
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "maven.modrinth:fabrictailor:1.5.0"
    implementation("me.hypherionmc.sdlink:sdlink-lib:${library_ver}h")
    include("me.hypherionmc.sdlink:sdlink-lib:${library_ver}h")
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

task delDevJar {
    doLast {
        def tree = fileTree('build/libs')
        tree.include '**/*-dev.jar'
        tree.each { it.delete() }
    }
}
build.finalizedBy delDevJar

modrinth {
    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "Sh0YauEf"
    versionNumber = "${minecraft_version}-${version}f"
    versionType = "release"
    changelog = rootProject.file("changelog-fabric.md").text.trim()
    uploadFile = remapJar
    versionName = "[Fabric/Quilt 1.16.5] Simple Discord Link - ${project.version}"
    gameVersions = ["1.16.5"]
    loaders = [ "fabric" ]
    dependencies {
        required.project "P7dR8mSH"
    }
}

curseforge {
    if (System.getenv("CURSE_TOKEN") !== null) {
        apiKey = System.getenv("CURSE_TOKEN")
    } else {
        apiKey = ""
    }
    project {
        id = "541320"
        releaseType = "release"
        addGameVersion "1.16.5"
        addGameVersion "Fabric"
        addGameVersion "Java 8"
        changelogType = "markdown"
        changelog = rootProject.file("changelog-fabric.md")
        mainArtifact(remapJar) {
            displayName = "[Fabric 1.16.5] Simple Discord Link - ${project.version}"
        }
    }
    options {
        forgeGradleIntegration = false
        debug = false
        fabricIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge541320.dependsOn remapJar
}
